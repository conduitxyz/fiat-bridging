// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

import {Predeploys} from "src/libraries/Predeploys.sol";
import {UsdcBridge} from "src/universal/FiatBridge.sol";
import {ISemver} from "src/universal/ISemver.sol";
import {CrossDomainMessenger} from "src/universal/CrossDomainMessenger.sol";
import {Constants} from "src/libraries/Constants.sol";

/// @custom:proxied
/// @title L2UsdcBridge
/// @notice The L2UsdcBridge is responsible for transfering FIAT tokens between L1 and
///         L2.
///         NOTE: this contract is not intended to support all variations of ERC20 tokens. Examples
///         of some token types that may not be properly supported by this contract include, but are
///         not limited to: tokens with transfer fees, rebasing tokens, and tokens with blocklists.
contract L2UsdcBridge is UsdcBridge, ISemver {
    /// @custom:legacy
    /// @notice Emitted whenever a withdrawal from L2 to L1 is initiated.
    /// @param l1Token   Address of the token on L1.
    /// @param l2Token   Address of the corresponding token on L2.
    /// @param from      Address of the withdrawer.
    /// @param to        Address of the recipient on L1.
    /// @param amount    Amount of the ERC20 withdrawn.
    /// @param extraData Extra data attached to the withdrawal.
    event WithdrawalInitiated(
        address indexed l1Token,
        address indexed l2Token,
        address indexed from,
        address to,
        uint256 amount,
        bytes extraData
    );

    /// @custom:legacy
    /// @notice Emitted whenever an ERC20 deposit is finalized.
    /// @param l1Token   Address of the token on L1.
    /// @param l2Token   Address of the corresponding token on L2.
    /// @param from      Address of the depositor.
    /// @param to        Address of the recipient on L2.
    /// @param amount    Amount of the ERC20 deposited.
    /// @param extraData Extra data attached to the deposit.
    event DepositFinalized(
        address indexed l1Token,
        address indexed l2Token,
        address indexed from,
        address to,
        uint256 amount,
        bytes extraData
    );

    /// @custom:semver 1.8.0
    string public constant version = "1.8.0";

    /// @notice Constructs the L2UsdcBridge contract.
    constructor() UsdcBridge() {}

    /// @notice Initializes the contract.
    /// @param _otherBridge The L1 bridge address.
    /// @param _l1Usdc      The ERC20 address on the L1.
    /// @param _l2Usdc      The ERC20 address on the L2.
    /// @param _owner       The initial owner of this contract.
    function initialize(
        UsdcBridge _otherBridge,
        address _l1Usdc,
        address _l2Usdc,
        address _owner
    ) public initializer {
        __UsdcBridge_init({
            _messenger: CrossDomainMessenger(
                Predeploys.L2_CROSS_DOMAIN_MESSENGER
            ),
            _otherBridge: _otherBridge,
            _l1Usdc: _l1Usdc,
            _l2Usdc: _l2Usdc,
            _owner: _owner
        });
    }

    /// @custom:legacy
    /// @notice Initiates a withdrawal from L2 to L1.
    ///         This function only works with Fiat
    /// @param _l2Token     Address of the L2 token to withdraw.
    /// @param _amount      Amount of the L2 token to withdraw.
    /// @param _minGasLimit Minimum gas limit to use for the transaction.
    /// @param _extraData   Extra data attached to the withdrawal.
    function withdraw(
        address _l2Token,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) external payable virtual onlyEOA {
        _initiateWithdrawal(
            _l2Token,
            msg.sender,
            msg.sender,
            _amount,
            _minGasLimit,
            _extraData
        );
    }

    /// @custom:legacy
    /// @notice Initiates a withdrawal from L2 to L1 to a target account on L1.
    ///         This function only works for fiat.
    /// @param _l2Token     Address of the L2 token to withdraw.
    /// @param _to          Recipient account on L1.
    /// @param _amount      Amount of the L2 token to withdraw.
    /// @param _minGasLimit Minimum gas limit to use for the transaction.
    /// @param _extraData   Extra data attached to the withdrawal.
    function withdrawTo(
        address _l2Token,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) external payable virtual {
        _initiateWithdrawal(
            _l2Token,
            msg.sender,
            _to,
            _amount,
            _minGasLimit,
            _extraData
        );
    }

    /// @custom:legacy
    /// @notice Finalizes a deposit from L1 to L2.
    /// @param _l1Token   Address of the L1 token to deposit.
    /// @param _l2Token   Address of the corresponding L2 token.
    /// @param _from      Address of the depositor.
    /// @param _to        Address of the recipient.
    /// @param _amount    Amount of the tokens being deposited.
    /// @param _extraData Extra data attached to the deposit.
    function finalizeDeposit(
        address _l1Token,
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _extraData
    ) external payable virtual {
        finalizeBridgeERC20(
            _l2Token,
            _l1Token,
            _from,
            _to,
            _amount,
            _extraData
        );
    }

    /// @custom:legacy
    /// @notice Retrieves the access of the corresponding L1 bridge contract.
    /// @return Address of the corresponding L1 bridge contract.
    function l1TokenBridge() external view returns (address) {
        return address(otherBridge);
    }

    /// @custom:legacy
    /// @notice Internal function to initiate a withdrawal from L2 to L1 to a target account on L1.
    /// @param _l2Token     Address of the L2 token to withdraw.
    /// @param _from        Address of the withdrawer.
    /// @param _to          Recipient account on L1.
    /// @param _amount      Amount of the L2 token to withdraw.
    /// @param _minGasLimit Minimum gas limit to use for the transaction.
    /// @param _extraData   Extra data attached to the withdrawal.
    function _initiateWithdrawal(
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes memory _extraData
    ) internal {
        _initiateBridgeERC20(
            _l2Token,
            l1Usdc,
            _from,
            _to,
            _amount,
            _minGasLimit,
            _extraData
        );
    }

    /// @notice Emits the legacy WithdrawalInitiated event followed by the ERC20BridgeInitiated
    ///         event. This is necessary for backwards compatibility with the legacy bridge.
    /// @inheritdoc UsdcBridge
    function _emitERC20BridgeInitiated(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _amount,
        bytes memory _extraData
    ) internal override {
        emit WithdrawalInitiated(
            _remoteToken,
            _localToken,
            _from,
            _to,
            _amount,
            _extraData
        );
        super._emitERC20BridgeInitiated(
            _localToken,
            _remoteToken,
            _from,
            _to,
            _amount,
            _extraData
        );
    }

    /// @notice Emits the legacy DepositFinalized event followed by the ERC20BridgeFinalized event.
    ///         This is necessary for backwards compatibility with the legacy bridge.
    /// @inheritdoc UsdcBridge
    function _emitERC20BridgeFinalized(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _amount,
        bytes memory _extraData
    ) internal override {
        emit DepositFinalized(
            _remoteToken,
            _localToken,
            _from,
            _to,
            _amount,
            _extraData
        );
        super._emitERC20BridgeFinalized(
            _localToken,
            _remoteToken,
            _from,
            _to,
            _amount,
            _extraData
        );
    }

    /// @inheritdoc UsdcBridge
    function _isCorrectUsdcTokenPair(
        address _localToken,
        address _remoteToken
    ) internal view override returns (bool) {
        return _isL2Usdc(_localToken) && _isL1Usdc(_remoteToken);
    }
}
